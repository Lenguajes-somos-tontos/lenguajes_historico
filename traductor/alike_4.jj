//*****************************************************************
// File:   alike.jj
// Author: Jorge Rodilla Esteve 845389 Enrique Martinez Casanova 839773
// Date:   marzo 2024
// Coms:   compilar mediante "ant"
//*****************************************************************

options {
	IGNORE_CASE = true;			// Comparaciones de tokens case-insensitive
	COMMON_TOKEN_ACTION = false;
	UNICODE_INPUT = true;
	//DEBUG_PARSER = true;
}

PARSER_BEGIN(alike)

package traductor;

import lib.symbolTable.*;
import lib.symbolTable.exceptions.*;
import java.util.*;
import lib.attributes.*;
import lib.tools.codeGeneration.*;
import java.io.File;
import java.io.FileWriter;
import java.io.BufferedWriter;
import java.io.IOException;

public class alike {
	static SymbolTable st;
	static SemanticFunction sf;
	public static CodeBlock bloque;


	public static int codigo_estado;
	public static int nivel_bloque;

	public static int[] sig;

	public static void codigo_error() {codigo_estado = 1;}

	public static String ruta;

	public static void main(String[] args) {
		st = new SymbolTable();
		sf = new SemanticFunction();
		codigo_estado = 0;
		nivel_bloque = 0;
		bloque = new CodeBlock();
		ruta = args[0] + ".pcode";

		sig = new int[SymbolTable.ST_SIZE];
		for (int i = 0; i < sig.length; i++) sig[i] = 3;

	   alike parser = null;
		try {
		    if (args.length == 0) {
			   parser = new alike(System.in);
			}
		    else {
			   parser = new alike(new java.io.FileInputStream(args[0] + ".al"));
		    }
		System.out.println("Compilando programa...");
		   parser.Programa();
	    }
	    catch (java.io.FileNotFoundException e) {
		   System.err.println ("Fichero " + args[0] + " no encontrado.");
	    }
	    catch (TokenMgrError e) {
		   System.err.println("LEXICAL ERROR: " + e.getMessage());
	    }
	    catch (ParseException e) {
		   System.err.println("PARSE ERROR: " + e.getMessage());
		}
	    catch (Exception e) {
			System.err.println("Finalizo traducción por detección de error no controlado: \n\t" + e.getMessage());
			System.err.println("Se ha producido la excepción: \n\t" + e.getClass().getName());
  		}
		  if (codigo_estado == 0) {
			System.out.println("Compilación finalizada. Se ha generado el fichero " +  args[0] + ".pcode");
		}
		else {
			System.out.println("Se han producido errores semánticos, no se genera código");
		}
		System.exit(codigo_estado);
	}
}

PARSER_END(alike)

TOKEN : {
	<#tLETRA: (["a"-"z"])>
|	<#tDIGITO: ["0"-"9"]>
|	<#tCARACTER: (~["\t","\n","\r","\""])>
}

TOKEN : {
	<tBOOL: "boolean">
|	<tCHAR: "character">
|	<tINT: "integer">
|   <tASIG: ":=">
|   <tDISTINTO: "/=">
|   <tIGUAL: "=">
|   <tMAYOR: ">">
|   <tMENOR: "<">
|   <tMENORIGUAL: "<=">
|   <tMAYORIGUAL: ">=">
|   <tOPMAS: "+">
|   <tOPMENOS: "-">
|   <tOPMULT: "*">
|   <tOPDIV: "/">
|   <tBARRAB: "_">
|   <tIF: "if">
|   <tAND: "and">
|   <tOR: "or">
|   <tNOT: "not">
|   <tTHEN: "then">
|   <tELSE: "else">
|   <tELSIF: "elsif">
|   <tPC: ";">
|   <tPUNTOS: "..">
|   <tCOMA: ",">
|   <tWHILE: "while">
|   <tTRUE: "true">
|   <tFALSE: "false">
|   <tLOOP: "loop">
|   <tPROC: "procedure">
|   <tFUNC: "function">
|   <tIS: "is">
|	<tBEGIN: "begin">
|	<tEND: "end">	
|	<tOPMOD: "mod">
|	<tOF: "of">
|	<tREF: "ref">
|	<tAP: "(">
|	<tCP: ")">
|	<tAC: "[">
|	<tCC: "]">
|	<tARRAY: "array">
|	<tDOSP: ":">
|	<tRETURN: "return">
|	<tNULL: "null">
|	<tEXIT: "exit">
|	<tINT2CHAR: "int2char">
|	<tCHAR2INT: "char2int">
|	<tNUM: (<tDIGITO>)+>
|	<tID: (<tLETRA > | <tBARRAB>) (<tLETRA> | <tDIGITO> | <tBARRAB>)*>
|	<tSTRING: "\""(<tCARACTER>)*"\"">
|	<tCONSTCHAR: "\'"<tCARACTER>"\'">
}

SKIP :
{
	" "
|	"\t"
|	"\n"
|	"\r"
|	<tCOMMENT : "--" ((<tCARACTER>) | ("\""))*>
}


void Programa() : 
{
}
{
	{st.insertBlock();}
	Programa_completo()
	{st.removeBlock();}
	<EOF>
}


void Declaracion() : // Cada linea de declaracion dispara esta produccion
{
	ArrayList<String> array_nombres_variables;	// Estas variables son del mismo tipo
	Symbol tipo_variables;						// UN simbolo sin nombre
	Token linea;
}
{
	{array_nombres_variables = new ArrayList<String>();}
	(Lista_IDS(array_nombres_variables) linea = <tDOSP> tipo_variables = Tipos_variables())
	{sf.Declaracion(array_nombres_variables, tipo_variables, st, linea);}
}


// Devuelve UN simbolo sin nombre del tipo correspondiente
Symbol Tipos_variables() :
{
	Symbol result;
}
{
	(result = Tipo_variable() | result = Tipo_array())
	{return result;}
}


// Array por referencia de IDs leidos
void Lista_IDS(ArrayList<String> array_nombres_variables) :
{
	Token t;
}
{
	t = <tID> {array_nombres_variables.add(t.image.toLowerCase());} (<tCOMA> Lista_IDS(array_nombres_variables))?
}


// Devuelve un puntero a símbolo sin nombre
Symbol Tipo_variable() :
{
	Token t;
	Symbol result = null;
}
{
	(t = <tBOOL> | t = <tINT> | t = <tCHAR>)
	{return sf.Tipo_variable(t, result);}
}


// Simbolo de tipo array sin nombre
Symbol Tipo_array() :
{
	Token indMin, indMax;
	Symbol tipo_base;
	Token ind_menor = null, ind_mayor = null;
}
{
	<tARRAY> <tAP> (ind_menor = <tOPMAS> | ind_menor = <tOPMENOS>)? indMin = <tNUM> <tPUNTOS>
	(ind_mayor = <tOPMAS> | ind_mayor = <tOPMENOS>)? indMax = <tNUM> <tCP> <tOF> tipo_base = Tipo_variable()
	{return sf.Tipo_array(indMin, indMax, tipo_base, ind_menor, ind_mayor);}
}


Trio Expresion() :
{
	Trio tipo;
	Trio tipo2 = new Trio();
	boolean match2 = false;
	Token t = null;
}
{
	tipo = Relacion() ((t = <tAND> tipo2 = Relacion()
	{
		sf.verificar_bool_expresion(tipo, tipo2, t);
	})+
	| (t = <tOR> tipo2 = Relacion()
	{
		sf.verificar_bool_expresion(tipo, tipo2, t);
	})+)?
	{return tipo;}
}


Trio Relacion() :
{
	Trio tipo;
	Trio tipo2 = new Trio();
	Token op = null;
	boolean match = false;
}
{
	tipo = Expresion_simple() (op = Operador_relacional() tipo2 = Expresion_simple() {match=true;})?
	{
		if (match) sf.verificar_relacion(op, tipo, tipo2);
		return tipo;
	}
}


Token Operador_relacional() :
{
	Token t;
}
{
    (t = <tIGUAL> | t = <tMENOR> | t = <tMAYOR> | t = <tMENORIGUAL> | t = <tMAYORIGUAL> | t = <tDISTINTO>)
	{return t;}
}


Token Operador_multiplicativo() :
{
	Token t;
}
{
    (t = <tOPMULT> | t = <tOPMOD> | t = <tOPDIV>)
	{return t;}
}


Trio Expresion_simple() :
{
	Trio tipo;
	Trio tipo2 = new Trio();
	boolean match1 = false;
	Token t1 = null;
	Token t2 = null;
}
{
	((t1 = <tOPMAS> | t1 = <tOPMENOS>) {match1=true;})? tipo = Termino()
	{
		if (t1 != null && t1.image.equals("-")) bloque.addInst(PCodeInstruction.OpCode.NGI);
	}
	((t2 = <tOPMAS> | t2 = <tOPMENOS>)  tipo2 = Termino()
	{sf.verificar_int(tipo, tipo2, t2);})*
	{
		if (match1) {
			tipo.referencia = false;
		}
		return tipo;
	}
}


Trio Termino() :
{
	Trio tipo = new Trio();
	Trio tipo2 = new Trio();
	Token t;
}
{
    tipo = Factor() (t = Operador_multiplicativo() tipo2 = Factor()
	{sf.verificar_int(tipo, tipo2, t);})*
	{return tipo;}
}


Trio Factor() :
{
	Trio tipo = new Trio();
	Trio tipo2 = new Trio();
	boolean match = false;
	Token t = null;
}
{
	(tipo = Primario() | t = <tNOT> tipo2 = Primario() {match = true;})
	{
		if (match) {
			tipo.referencia = false;
			if (tipo2.tipo != Symbol.Types.BOOL) {
				tipo.tipo = Symbol.Types.UNDEFINED;
				sf.esperaba_tipo(Symbol.Types.BOOL, t.beginLine, t.beginColumn);
			}
			else {
				tipo.tipo = Symbol.Types.BOOL;
				bloque.addInst(PCodeInstruction.OpCode.NGB);
			}
		}
		return tipo;
	}
}


Trio Primario() :
{
	Token t;
	Trio tipo = new Trio();
	Llamada llamada;
	Trio argumento;
}
{
    (<tAP> tipo = Expresion() <tCP>
|	t = <tCHAR2INT> <tAP> tipo = Expresion() <tCP>
	{
		if (tipo.tipo != Symbol.Types.CHAR) sf.esperaba_tipo(Symbol.Types.CHAR, t.beginLine, t.beginColumn);
		else tipo.tipo = Symbol.Types.INT;
	}

|	t = <tINT2CHAR> <tAP> tipo = Expresion() <tCP>
	{
		if (tipo.tipo != Symbol.Types.INT) sf.esperaba_tipo(Symbol.Types.INT, t.beginLine, t.beginColumn);
		else tipo.tipo = Symbol.Types.CHAR;
	}

|	LOOKAHEAD(2) t = <tID> <tAP> argumento = Expresion()
	{
		boolean error_array = false, error_args = false;
		int iterador_argumentos = 0;

		llamada = sf.verificar_func_array(t, st);
		ArrayList<Symbol> lista_parametros = llamada.lista;

		// Se ha leído el ID de una función
		if (llamada.simbolo != null && llamada.simbolo.type == Symbol.Types.FUNCTION) {
			SymbolFunction simbolo_funcion = (SymbolFunction) llamada.simbolo;
			tipo.tipo = simbolo_funcion.returnType;
			if (0 < lista_parametros.size()) {
				sf.verificar_argumento(argumento, lista_parametros, 0, st, t);
			}
			else {
				error_args = true;
			}
		}
		// Se ha leído el ID de un array
		else if (llamada.simbolo != null && llamada.simbolo.type == Symbol.Types.ARRAY) {
			SymbolArray s = (SymbolArray) llamada.simbolo;
			if (argumento.tipo == Symbol.Types.INT) {
				tipo.tipo = s.baseType;
				tipo.referencia = true;
				bloque.addInst(PCodeInstruction.OpCode.STC, s.minInd);
				bloque.addInst(PCodeInstruction.OpCode.SBT);
				bloque.addInst(PCodeInstruction.OpCode.SRF, nivel_bloque - s.nivel, (int)s.dir);
				if (s.parClass == Symbol.ParameterClass.REF) bloque.addInst(PCodeInstruction.OpCode.DRF);
				bloque.addInst(PCodeInstruction.OpCode.PLUS);
				bloque.addInst(PCodeInstruction.OpCode.DRF);
			}
			else {
				sf.esperaba_tipo(Symbol.Types.INT, t.beginLine, t.beginColumn);
			}
		}
		// Sólo se consideran los casos de función y array, si el ID es de otra cosa,
		// se ha sacado ya el error en "verificar_func_array()"
	}
	(<tCOMA> argumento = Expresion()
	{
		iterador_argumentos++;

		if (llamada.simbolo != null && llamada.simbolo.type == Symbol.Types.FUNCTION) {
			if (iterador_argumentos < lista_parametros.size()) {
				sf.verificar_argumento(argumento, lista_parametros, iterador_argumentos, st, t);
			}
			else {
				error_args = true;
			}
		}
		else if (llamada.simbolo != null && llamada.simbolo.type == Symbol.Types.ARRAY) {
			error_array = true;
		}
	})* <tCP>
	{
		if (llamada.simbolo != null && llamada.simbolo.type == Symbol.Types.FUNCTION) {
			SymbolFunction simbolo_funcion = (SymbolFunction) llamada.simbolo;
			bloque.addOSFInst(sig[nivel_bloque], nivel_bloque - simbolo_funcion.nivel, simbolo_funcion.label);
		}
		if (lista_parametros != null && iterador_argumentos+1 != lista_parametros.size()) {
			error_args = true;
		}
		if (error_array) {
			sf.error("Se esperaba un único índice", t.beginLine, t.beginColumn);
			tipo.tipo = Symbol.Types.UNDEFINED;
		}
		else if (error_args) {
			sf.error("Se esperaban " + lista_parametros.size() + " argumentos", t.beginLine, t.beginColumn);
			tipo.tipo = Symbol.Types.UNDEFINED;
		}
	}

|	t = <tID> {sf.Primario_ID(t, tipo, st);}

|	t = <tNUM>
	{
		tipo.tipo = Symbol.Types.INT;
		bloque.addInst(PCodeInstruction.OpCode.STC, Integer.parseInt(t.image));
	}

|	t = <tCONSTCHAR>
	{
		char c = t.image.charAt(1);
		tipo.tipo = Symbol.Types.CHAR;
		tipo.nombre = String.valueOf(c);
		if (c < 128) {
			bloque.addInst(PCodeInstruction.OpCode.STC, (int) c);
		}
	}

|	t = <tSTRING>
	{
		tipo.tipo = Symbol.Types.STRING;
		tipo.nombre = t.image;
	}

|	t = <tTRUE>
	{
		tipo.tipo = Symbol.Types.BOOL;
		bloque.addInst(PCodeInstruction.OpCode.STC, 1);
	}

|	t = <tFALSE>
	{
		tipo.tipo = Symbol.Types.BOOL;
		bloque.addInst(PCodeInstruction.OpCode.STC, 0);
	})
	{return tipo;}
}


void Asignacion() :
{
	Token id;
	Trio tipo_asignacion;
	Token t;
}
{
	id = <tID> t = <tASIG> tipo_asignacion = Expresion()
	{sf.Asignacion(id.image.toLowerCase(), tipo_asignacion, st, t);}
}


void Instruccion_sin_return() :
{
	Token id = null;
	boolean llamada_funcion_simple = true;
	ArrayList<Symbol> lista_param = null;
}
{
	(LOOKAHEAD(2) Asignacion() // Asignacion a variable no array
	| LOOKAHEAD(2) id = <tID> (Instr_funcion_vector(id.image.toLowerCase(), id) {llamada_funcion_simple = false;})?
	{
		String idd = id.image.toLowerCase();
		sf.void_sin_parametros(llamada_funcion_simple, idd, id, st);
	}
	| Instr_condicional_sin_return() | Instr_iterativa_sin_return()
	| <tEXIT>
	| <tNULL>) <tPC>
}


boolean Instruccion_con_return(Symbol.Types tipo_a_retornar) :
{
	Token id = null;
	boolean llamada_funcion_simple = true;
	Trio tipo_return = null;
	boolean es_return = false;
	boolean return_if = false;
	Token t = null;
	ArrayList<Symbol> lista_param = null;
}
{
	(LOOKAHEAD(2) Asignacion() // Asignacion a variable no array
	| LOOKAHEAD(2) id = <tID> (Instr_funcion_vector(id.image.toLowerCase(), id) {llamada_funcion_simple = false;})?
	{
		String idd = id.image.toLowerCase();
		sf.void_sin_parametros(llamada_funcion_simple, idd, id, st);
	}
	| return_if = Instr_condicional_con_return(tipo_a_retornar) | Instr_iterativa_con_return(tipo_a_retornar)
	| <tNULL>
	| <tEXIT>
	| t = <tRETURN> tipo_return = Expresion() {es_return = true;})
	<tPC>
	{return es_return || return_if;}
}


void Instr_funcion_vector(String id, Token t) :
{
	Trio tipo;
}
{
	<tAP> tipo = Expresion() Instr_funcion_vector_2(id, tipo, t)
}


void Instr_funcion_vector_2(String id, Trio tipo, Token t) :
{
	Trio argumento;
	int iterador_argumentos = 0;
	int argumentos_leidos = 1;
	boolean error_sacado = false;
	Llamada l = null;
}
{
	t = <tCP> Instr_funcion_vector_3(id, tipo, t)

	|	// Matching de llamadas a procedimientos con más de un argumento
	{
		l = sf.proc_param(id, t, st);
		if (l.lista != null) {
			if (iterador_argumentos < l.lista.size()) {
				sf.verificar_argumento(tipo, l.lista, iterador_argumentos, st, t);
			}
			else {	// Hay más argumentos que parámetros
				error_sacado = true;
				sf.error("Se esperaban " + l.lista.size() + " argumentos", t.beginLine, t.beginColumn);
			}
		}
		sf.procedimientos_especiales(id, tipo, t);
		iterador_argumentos++;
	}
	(t = <tCOMA> argumento = Expresion()
	{
		argumentos_leidos++;
		if (l.lista != null) {
			if (iterador_argumentos < l.lista.size()) {
				sf.verificar_argumento(argumento, l.lista, iterador_argumentos, st, t);
			}
			else if (!error_sacado) {	// Hay más argumentos que parámetros
				error_sacado = true;
				sf.error("Se esperaban " + l.lista.size() + " argumentos", t.beginLine, t.beginColumn);
			}
		}
		sf.procedimientos_especiales(id, argumento, t);
		iterador_argumentos++;
	})+ <tCP>
	{
		if (id.equals("put_line")) {
			bloque.addInst(PCodeInstruction.OpCode.STC, 13);
			bloque.addInst(PCodeInstruction.OpCode.WRT, 0);
			bloque.addInst(PCodeInstruction.OpCode.STC, 10);
			bloque.addInst(PCodeInstruction.OpCode.WRT, 0);
		}
		else if (l.lista != null && argumentos_leidos != l.lista.size() && !error_sacado) {
			// Hay más parámetros que argumentos
			sf.error("Se esperaban " + l.lista.size() + " argumentos", t.beginLine, t.beginColumn);
		}
		if (l.simbolo != null && l.simbolo.type == Symbol.Types.PROCEDURE) {
			SymbolProcedure s = (SymbolProcedure) l.simbolo;
			bloque.addOSFInst(sig[nivel_bloque], nivel_bloque - s.nivel, s.label);
		}
	}
}


void Instr_funcion_vector_3(String id, Trio primera_expresion, Token t) :
{
	Trio tipo_asignacion;
	Llamada l = null;
	Symbol.Types tipo_base_array;
}
{
	// Matching de asignacion a componente de array, "primera_expresion" es la expresión del índice

	t = <tASIG> {tipo_base_array = sf.direccion_array(id, st, primera_expresion, t);} tipo_asignacion = Expresion()
	{
		sf.Instr_funcion_vector_3(tipo_asignacion, tipo_base_array, t);
	}

	| {}	// Matching de llamada a procedimiento con UN argumento
	{
		l = sf.proc_param(id, t, st);
		if (l.lista != null) {
			if (l.lista.size() == 1) {
				sf.verificar_argumento(primera_expresion, l.lista, 0, st, t);
			}
			else {	// El número de argumentos no coincide con el de parámetros
				sf.error("Se esperaban " + l.lista.size() + " argumentos", t.beginLine, t.beginColumn);
			}
		}
		sf.procedimientos_especiales(id, primera_expresion, t);
		if (l.lista != null && l.simbolo.type == Symbol.Types.PROCEDURE) {
			SymbolProcedure s = (SymbolProcedure) l.simbolo;
			bloque.addOSFInst(sig[nivel_bloque], nivel_bloque - s.nivel, s.label);
		}
		if (id.equals("put_line")) {
			bloque.addInst(PCodeInstruction.OpCode.STC, 13);
			bloque.addInst(PCodeInstruction.OpCode.WRT, 0);
			bloque.addInst(PCodeInstruction.OpCode.STC, 10);
			bloque.addInst(PCodeInstruction.OpCode.WRT, 0);
		}
	}
}


void Programa_completo() :
{
}
{
	{
		String label = CGUtils.newLabel();
		bloque.addInst(PCodeInstruction.OpCode.ENP, label);
	}
	<tPROC> <tID> <tIS>
	(Declaracion() <tPC>)*
	(Procedimiento() | Funcion())*
	<tBEGIN>
	{
		//bloque.addComment(" Comienzo de main");
		bloque.addLabel(label);
	}
	(Instruccion_sin_return())+
	{
		//bloque.addComment(" Fin de main");
		bloque.addInst(PCodeInstruction.OpCode.LVP);
		if (codigo_estado == 0) {
			try {
				File archivo = new File(ruta);
				BufferedWriter escritor = new BufferedWriter(new FileWriter(archivo));
				escritor.write(bloque.toString());
				escritor.close();
			}
			catch (IOException e) {}
		}
		bloque.clearBlock();
	}
	<tEND> <tPC>
}


// Devuelve un Symbol del tipo en cuestión, por valor o por referencia y sin nombre
Symbol Parametro_aux() :
{
	boolean por_referencia;
	Symbol tipo_parametro;
}
{
	({por_referencia = true;} <tREF> | {por_referencia = false;}) tipo_parametro = Tipos_variables() // Tipos_variables() devuelve UN symbol sin nombre de un tipo
	{
		if (por_referencia) tipo_parametro.parClass = Symbol.ParameterClass.REF;
		else tipo_parametro.parClass = Symbol.ParameterClass.VAL;
		return tipo_parametro;
	}
}


// Array por referencia con los símbolos leídos de parámetros
void Parametro(ArrayList<Symbol> lista_parametros) :
{
	ArrayList<String> array_nombres_variables = new ArrayList<>();
	Symbol tipo_parametros;
}
{
	Lista_IDS(array_nombres_variables) <tDOSP> tipo_parametros = Parametro_aux()
	{
		// Misma lógica que Declaracion(), se recoge un puntero a símbolo sin nombre y una lista de Strings,
		// se le asigna un nombre al puntero y se mete una copia en el array de simbolos
		for (String nombre_parametro : array_nombres_variables) {
			tipo_parametros.name = nombre_parametro.toLowerCase();
			lista_parametros.add(tipo_parametros.clone());
		}
	}
}


// Similar a Declaracion(), inserta en la tabla de simbolos un array de símbolos (la lista de parametros) y lo retorna
ArrayList<Symbol> Parametros() :
{
	ArrayList<Symbol> lista_parametros = new ArrayList<>();
	Token t = null;
}
{
	(t = <tAP> Parametro(lista_parametros) (<tPC> Parametro(lista_parametros))* <tCP>)?
	{
		for (Symbol simbolo : lista_parametros) {
			try {
				st.insertSymbol(simbolo);
				simbolo.nivel = simbolo.nivel - 1;
				simbolo.dir = sig[nivel_bloque];
				if (simbolo.type == Symbol.Types.ARRAY && simbolo.parClass == Symbol.ParameterClass.VAL) {
					SymbolArray s = (SymbolArray) simbolo;
					alike.sig[alike.nivel_bloque] += (s.maxInd - s.minInd + 1);
				}
				else
					alike.sig[alike.nivel_bloque]++;
			}
			catch (AlreadyDefinedSymbolException a) {
				sf.simbolo_definido(simbolo.name, t.beginLine, t.endColumn);
			}
			catch (IndexArrayNotCorrect i) {
				sf.error("Los indices del array " + simbolo.name + " no son correctos", t.beginLine, t.endColumn);
			}
		}
		return lista_parametros;
	}
}


void Procedimiento() :
{
	Token nombre_procedimiento;
	SymbolProcedure simbolo_procedimiento;
}
{
	<tPROC> nombre_procedimiento = <tID>
	// Antes de leer la lista de parametros se inserta el simbolo procedimiento y se crea el bloque de parametros y variables locales
	{
		String label = CGUtils.newLabel();
		// Se crea el simbolo procedimiento sin parametros, que van un nivel por debajo
		simbolo_procedimiento = new SymbolProcedure(nombre_procedimiento.image.toLowerCase(), new ArrayList<Symbol>(), label);
		try {
			st.insertSymbol(simbolo_procedimiento);
			simbolo_procedimiento.nivel = simbolo_procedimiento.nivel-1;
		}
		catch (AlreadyDefinedSymbolException a) {
			sf.simbolo_definido(nombre_procedimiento.image, nombre_procedimiento.beginLine, nombre_procedimiento.beginColumn);
		}
		catch (IndexArrayNotCorrect i) {}	// Se sabe que el simbolo no es un array
		st.insertBlock();
		nivel_bloque++;
	}
	simbolo_procedimiento.parList = Parametros() <tIS>	// Se asigna al simbolo del procedimiento la lista de parametros
	(Declaracion() <tPC>)*
	{
		bloque.addLabel(label);
		for (int i = simbolo_procedimiento.parList.size() - 1; i >= 0; i--) {
			Symbol simbolo = simbolo_procedimiento.parList.get(i);
			if (simbolo.type == Symbol.Types.ARRAY && simbolo.parClass == Symbol.ParameterClass.VAL) {
				SymbolArray s = (SymbolArray) simbolo;
				int dirArray = (int)simbolo.dir + (s.maxInd - s.minInd);
				for (int j = (s.maxInd - s.minInd); j >= 0; j--) {
					bloque.addInst(PCodeInstruction.OpCode.SRF, alike.nivel_bloque - simbolo.nivel, dirArray);
					bloque.addInst(PCodeInstruction.OpCode.ASGI);
					dirArray--;
				}
			}
			else {
				bloque.addInst(PCodeInstruction.OpCode.SRF, alike.nivel_bloque - simbolo.nivel, (int)simbolo.dir);
				bloque.addInst(PCodeInstruction.OpCode.ASGI);
			}
		}
		String label2 = CGUtils.newLabel();
		bloque.addInst(PCodeInstruction.OpCode.JMP, label2);
	}
	(Procedimiento() | Funcion())*
	<tBEGIN>
	{
		bloque.addLabel(label2);
	}
	(Instruccion_sin_return())+
	{
		bloque.addInst(PCodeInstruction.OpCode.CSF);
	}
	<tEND> <tPC>
	{
		st.removeBlock();
		sig[nivel_bloque] = 3;
		nivel_bloque--;
	}
}


// Igual a Procedimiento(), sólo cambia el return
void Funcion() :
{
	Token nombre_funcion;
	Symbol tipo_return;
	SymbolFunction simbolo_funcion;
	boolean return_valido = false;
	boolean instruccion;
	Token t;
}
{
	<tFUNC> nombre_funcion = <tID>
	{
		String label = CGUtils.newLabel();
		simbolo_funcion = new SymbolFunction(nombre_funcion.image, new ArrayList<Symbol>(), Symbol.Types.UNDEFINED, label);
		try {
			st.insertSymbol(simbolo_funcion);
			simbolo_funcion.nivel = simbolo_funcion.nivel-1;
		}
		catch (AlreadyDefinedSymbolException a) {
			sf.simbolo_definido(nombre_funcion.image, nombre_funcion.beginLine, nombre_funcion.beginColumn);
		}
		catch (IndexArrayNotCorrect i) {}
		st.insertBlock();
		nivel_bloque++;
	}
	// Se asigna la lista de parámetros, se recoge el tipo que retorna la función y se asigna al simbolo función
	simbolo_funcion.parList = Parametros() <tRETURN> tipo_return = Tipo_variable() <tIS> {simbolo_funcion.returnType = tipo_return.type;}
	(Declaracion() <tPC>)*
	{
		bloque.addLabel(label);
		for (int i = simbolo_funcion.parList.size() - 1; i >= 0; i--) {
			Symbol simbolo = simbolo_funcion.parList.get(i);
			if (simbolo.type == Symbol.Types.ARRAY && simbolo.parClass == Symbol.ParameterClass.VAL) {
				SymbolArray s = (SymbolArray) simbolo;
				int dirArray = (int)simbolo.dir + (s.maxInd - s.minInd);
				for (int j = (s.maxInd - s.minInd); j >= 0; j--) {
					bloque.addInst(PCodeInstruction.OpCode.SRF, alike.nivel_bloque - simbolo.nivel, dirArray);
					bloque.addInst(PCodeInstruction.OpCode.ASGI);
					dirArray--;
				}
			}
			else {
				bloque.addInst(PCodeInstruction.OpCode.SRF, alike.nivel_bloque - simbolo.nivel, (int)simbolo.dir);
				bloque.addInst(PCodeInstruction.OpCode.ASGI);
			}
		}
		String label2 = CGUtils.newLabel();
		bloque.addInst(PCodeInstruction.OpCode.JMP, label2);
	}
	(Procedimiento() | Funcion())*
	<tBEGIN>
	{
		bloque.addLabel(label2);
	}
	(instruccion = Instruccion_con_return(tipo_return.type)
	{
		if (instruccion) return_valido = true;
	})+
	{
		bloque.addInst(PCodeInstruction.OpCode.CSF);
	}
	<tEND> t = <tPC>
	{
		if (!return_valido) System.out.println("AVISO (" + Integer.toString(t.beginLine) + ", " + Integer.toString(t.endColumn) + "): Es posible que la función no alcance ningún return");
		st.removeBlock();
		sig[nivel_bloque] = 3;
		nivel_bloque--;
	}
}


void Instr_iterativa_sin_return():
{
	Trio tipo;
	Token t;
}
{
	{
		String label2 = CGUtils.newLabel();
		bloque.addLabel(label2);
	}
	t = <tWHILE> tipo = Expresion() <tLOOP>
    {
        sf.verificar_bool(tipo.tipo, t);
        String label = CGUtils.newLabel();
        bloque.addInst(PCodeInstruction.OpCode.JMF, label);
    }
	(Instruccion_sin_return())+
	{
        bloque.addInst(PCodeInstruction.OpCode.JMP, label2);
		bloque.addLabel(label);
    }
	<tEND> <tLOOP>
}


void Instr_condicional_sin_return():
{
    Trio tipo;
    Token t;
}
{
    t = <tIF> tipo = Expresion() <tTHEN>
    {
        sf.verificar_bool(tipo.tipo, t);
        String label = CGUtils.newLabel();
        bloque.addInst(PCodeInstruction.OpCode.JMF, label);
    }
    (Instruccion_sin_return())+
    {
        String label2 = CGUtils.newLabel();
        bloque.addInst(PCodeInstruction.OpCode.JMP, label2);
        bloque.addLabel(label);
    }
    MasIF(label2)
	{bloque.addLabel(label2);}
    <tEND> <tIF>
}


void MasIF(String label2):
{
    Trio tipo;
    Token t;
}
{
    (t = <tELSIF> tipo = Expresion() <tTHEN>
    {
        sf.verificar_bool(tipo.tipo, t);
        String label = CGUtils.newLabel();
        bloque.addInst(PCodeInstruction.OpCode.JMF, label);
    }
    (Instruccion_sin_return())+
    {
        bloque.addInst(PCodeInstruction.OpCode.JMP, label2);
        bloque.addLabel(label);
    }
    MasIF(label2)
    | <tELSE> (Instruccion_sin_return())+
    | {})
}


void Instr_iterativa_con_return(Symbol.Types tipo_a_retornar):
{
	Trio tipo;
	Token t;
}
{
	{
		String label2 = CGUtils.newLabel();
		bloque.addLabel(label2);
	}
	t = <tWHILE> tipo = Expresion() <tLOOP>
	{
        sf.verificar_bool(tipo.tipo, t);
        String label = CGUtils.newLabel();
        bloque.addInst(PCodeInstruction.OpCode.JMF, label);
    }
	(Instruccion_con_return(tipo_a_retornar))+
	{
        bloque.addInst(PCodeInstruction.OpCode.JMP, label2);
		bloque.addLabel(label);
    }
	<tEND> <tLOOP>
}


boolean Instr_condicional_con_return(Symbol.Types tipo_a_retornar):
{
	Trio tipo;
	Token t;
	boolean es_return = false;
	boolean return_leido = false;
	boolean return_else = false;
}
{
	t = <tIF> tipo = Expresion() <tTHEN>
	{
		sf.verificar_bool(tipo.tipo, t);
		String label = CGUtils.newLabel();
		bloque.addInst(PCodeInstruction.OpCode.JMF, label);
	}
	(es_return = Instruccion_con_return(tipo_a_retornar) {if (es_return) return_leido = true;})+
	{
        String label2 = CGUtils.newLabel();
        bloque.addInst(PCodeInstruction.OpCode.JMP, label2);
        bloque.addLabel(label);
	}
	return_else = MasIF_return(tipo_a_retornar, label2)
	{bloque.addLabel(label2);}
	<tEND> <tIF>
	{return return_leido && return_else;}
}


boolean MasIF_return(Symbol.Types tipo_a_retornar, String label2):
{
	Trio tipo;
	Token t;
	boolean es_return = false;
	boolean return_leido = false;
}
{
	(t = <tELSIF> tipo = Expresion() <tTHEN>
	{
        sf.verificar_bool(tipo.tipo, t);
        String label = CGUtils.newLabel();
        bloque.addInst(PCodeInstruction.OpCode.JMF, label);
    }
	(es_return = Instruccion_con_return(tipo_a_retornar) {if (es_return) return_leido = true;})+
	{
        bloque.addInst(PCodeInstruction.OpCode.JMP, label2);
        bloque.addLabel(label);
    }
	es_return = MasIF_return(tipo_a_retornar, label2)

	| <tELSE> (es_return = Instruccion_con_return(tipo_a_retornar) {if (es_return) return_leido = true;})+
	| {} {return_leido = false;})
	{return return_leido && es_return;}
}
