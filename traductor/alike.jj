//*****************************************************************
// File:   alike.jj
// Author: Jorge Rodilla Esteve 845389 Enrique Martinez Casanova 839773
// Date:   marzo 2024
// Coms:   compilar mediante "ant"
//*****************************************************************

options {
	IGNORE_CASE = true;			// Esto hace que las comparaciones de tokens sea case-insensitive
	COMMON_TOKEN_ACTION = false;
}

PARSER_BEGIN(alike)

package traductor;

import lib.symbolTable.*;
import lib.symbolTable.exceptions.*;
import java.util.*;
import lib.attributes.*;

public class alike {
	static SymbolTable st;
	static SemanticFunction sf;

	public static void main(String[] args) {
		st = new SymbolTable();
		sf = new SemanticFunction();

	   alike parser = null;
		try {
		    if (args.length == 0) {
			   parser = new alike(System.in);
			}
		    else {
			   parser = new alike(new java.io.FileInputStream(args[0]));
		    }
		   parser.Programa();
		   System.out.println("***** Análisis terminado *****");
	    }
	    catch (java.io.FileNotFoundException e) {
		   System.err.println ("Fichero " + args[0] + " no encontrado.");
	    }
	    catch (TokenMgrError e) {
		   System.err.println("LEXICAL ERROR: " + e.getMessage());
	    }
	    catch (ParseException e) {
		   System.err.println("PARSE ERROR: " + e.getMessage());
		}
	    catch (Exception e) {
			System.err.println("Finalizo traducción por detección de error no controlado: \n\t" + e.getMessage());
  		}
	}
}

PARSER_END(alike)

TOKEN : {
	<#tLETRA: (["a"-"z"])>
|	<#tDIGITO: ["0"-"9"]>
|	<#tCARACTER: (~["\t","\n","\r"])>
}

TOKEN : {
	<tBOOL: "boolean">
|	<tCHAR: "character">
|	<tINT: "integer">
|   <tASIG: ":=">
|   <tDISTINTO: "/=">
|   <tIGUAL: "=">
|   <tMAYOR: ">">
|   <tMENOR: "<">
|   <tMENORIGUAL: "<=">
|   <tMAYORIGUAL: ">=">
|   <tOPMAS: "+">
|   <tOPMENOS: "-">
|   <tOPMULT: "*">
|   <tOPDIV: "/">
|   <tBARRAB: "_">
|   <tIF: "if">
|   <tAND: "and">
|   <tOR: "or">
|   <tNOT: "not">
|   <tTHEN: "then">
|   <tELSE: "else">
|   <tELSIF: "elsif">
|   <tPC: ";">
|   <tPUNTOS: "..">
|   <tCOMA: ",">
|   <tWHILE: "while">
|   <tTRUE: "true">
|   <tFALSE: "false">
|   <tLOOP: "loop">
|   <tPROC: "procedure">
|   <tFUNC: "function">
|   <tIS: "is">
|	<tBEGIN: "begin">
|	<tEND: "end">	
|	<tOPMOD: "mod">
|	<tOF: "of">
|	<tREF: "ref">
|	<tAP: "(">
|	<tCP: ")">
|	<tAC: "[">
|	<tCC: "]">
|	<tARRAY: "array">
|	<tDOSP: ":">
|	<tRETURN: "return">
|	<tNULL: "null">
|	<tNUM: (<tDIGITO>)+>
|	<tID: (<tLETRA > | <tBARRAB>) (<tLETRA> | <tDIGITO> | <tBARRAB>)*>
|	<tSTRING: "\""(<tCARACTER>)*"\"">
|	<tCONSTCHAR: "\'"<tCARACTER>"\'">
}

SKIP :
{
	" "
|	"\t"
|	"\n"
|	"\r"
|	<tCOMMENT : "--" (<tCARACTER>)*>
}



void Programa() : 
{
}
{

	////////////////////////////////////////////////////////////////////////////
	// LISTA DE AVANCES
	////////////////////////////////////////////////////////////////////////////
	/*
	 * - La produccion Declaracion() ya mete automaticamente los simbolos en la tabla,
	 *   no mete los que ya estan definidos a ese nivel, y saca mensajes de error por pantalla
	 *   en caso de que una variable ya este definida o en caso de que un array tenga mal los indices (ind_menor > ind_mayor).
	 *
	 * - La producción Parametros() devuelve la lista de símbolos leídos de parámetros sin checkear repetidos
	 *   o índices de array incorrectos y los inserta en la tabla de simbolos.
	 *
	 * - Procedimiento() y Funcion() funcionan igual, se mete el símbolo al mismo nivel de lo anterior, nuevo
	 *   bloque para variables globales y parámetros, si hay cualquier problema (nombre de variable, parámetro o función
	 *   ya repetido, o índices de algún array por parámetro incorrectos), se inserta lo que se pueda en la tabla de símbolos
	 *   (da igual lo que se meta porque el procedimiento o función está mal) y se asigna el array de parámetros.
	 */
	{st.insertBlock();}
	Programa_completo()
	{st.removeBlock();}
	<EOF>
}



void Declaracion() : // Cada linea de declaracion dispara esta produccion
{
	ArrayList<String> array_nombres_variables;	// Estas variables son del mismo tipo
	Symbol tipo_variables;						// UN simbolo sin nombre
}
{
	{array_nombres_variables = new ArrayList<String>();}

	(Lista_IDS(array_nombres_variables) <tDOSP> tipo_variables = Tipos_variables())

	{sf.Declaracion(array_nombres_variables, tipo_variables, st);}
}



// Devuelve UN simbolo sin nombre del tipo correspondiente
Symbol Tipos_variables() :
{
	Symbol result;
}
{
	(result = Tipo_variable() | result = Tipo_array())
	{return result;}
}



// Array por referencia de IDs leidos
void Lista_IDS(ArrayList<String> array_nombres_variables) :
{
	Token t;
}
{
	t = <tID> {array_nombres_variables.add(t.image.toLowerCase());} (<tCOMA> Lista_IDS(array_nombres_variables))?
}



// Devuelve un puntero a símbolo sin nombre
Symbol Tipo_variable() :
{
	Token t;
	Symbol result = null;
}
{
	(t = <tBOOL> | t = <tINT> | t = <tCHAR>)
	{return sf.Tipo_variable(t, result);}
}



// Simbolo de tipo array sin nombre
Symbol Tipo_array() :
{
	Token indMin, indMax;
	Symbol tipo_base;
}
{
	<tARRAY> <tAP> indMin = <tNUM> <tPUNTOS> indMax = <tNUM> <tCP> <tOF> tipo_base = Tipo_variable()
	{return sf.Tipo_array(indMin, indMax, tipo_base);}
}



///////////////////////////////////////////////////////
///////////////////////////////////////////////////////
///////////////////////////////////////////////////////
Par Expresion() :
{
	Par tipo;
	Par tipo2 = new Par(Symbol.Types.UNDEFINED, false);
	boolean match2 = false;
}
{
	//tipo = Relacion() (((<tAND> | <tOR>) tipo2 = Relacion())+)?
	tipo = Relacion() ((<tAND> | <tOR>) tipo2 = Relacion()
	{
		tipo.segundo = false;
		if (tipo.primero != Symbol.Types.BOOL || tipo2.primero != Symbol.Types.BOOL) {
			System.out.println("ERROR: Se esperaba un tipo BOOL");
			tipo.primero = Symbol.Types.UNDEFINED;
		}
	})*
	{
		System.out.println("Expresion: " + tipo.primero + " " + tipo.segundo);
		return tipo;
		//return sf.Expresion(tipo, tipo2, match2);
	}
}


Par Relacion() :
{
	Par tipo;
	Par tipo2 = new Par(Symbol.Types.UNDEFINED, false);
	Token op = null;
	boolean match = false;
}
{
	tipo = Expresion_simple() (op = Operador_relacional() tipo2 = Expresion_simple() {match=true;})?
	{
		if (match) {
			tipo.segundo = false;
			String operador = op.image;
			if (operador.equals("<") || operador.equals(">") || operador.equals("<=") || operador.equals(">=")) {
				if (tipo.primero != Symbol.Types.INT || tipo2.primero != Symbol.Types.INT) {
					tipo.primero = Symbol.Types.UNDEFINED;
					System.out.println("ERROR: Se esperaba un tipo INT");
				}
				else tipo.primero = Symbol.Types.BOOL;
			}
			else if (tipo2.primero != Symbol.Types.ARRAY || tipo2.primero != Symbol.Types.FUNCTION ||
					tipo2.primero != Symbol.Types.PROCEDURE || tipo2.primero != Symbol.Types.STRING) {
				if (tipo.primero != tipo2.primero) {
					tipo.primero = Symbol.Types.UNDEFINED;
					System.out.println("ERROR: Se esperaba un tipo " + tipo.primero);
				}
				else tipo.primero = Symbol.Types.BOOL;
			}
			else {
				System.out.println("ERROR: Se esperaba un tipo INT/BOOL/CHAR");
			}
		}
		return tipo;
		//return sf.Relacion(tipo, tipo2, op, match2);
	}
}


Token Operador_relacional() :
{
	Token t;
}
{
    (t = <tIGUAL> | t = <tMENOR> | t = <tMAYOR> | t = <tMENORIGUAL> | t = <tMAYORIGUAL> | t = <tDISTINTO>)
	{
		return t;
	}
}


void Operador_multiplicativo() :
{
}
{
    <tOPMULT> | <tOPMOD> | <tOPDIV>
}


Par Expresion_simple() :
{
	Par tipo;
	Par tipo2 = new Par(Symbol.Types.UNDEFINED, false);
	boolean match1 = false;
}
{
	(<tOPMAS> {match1=false;} | <tOPMENOS> {match1=false;})?
	tipo = Termino() ((<tOPMAS> | <tOPMENOS>)  tipo2 = Termino()
	{
		tipo.segundo = false;
		if (tipo.primero != Symbol.Types.INT || tipo2.primero != Symbol.Types.INT) {
			System.out.println("ERROR: Se esperaba un tipo INT");
			tipo.primero = Symbol.Types.UNDEFINED;
		}
	})*
	{
		if (match1) tipo.segundo = false;
		return tipo;
		//return sf.Expresion_simple(tipo, tipo2, match1, match2);
	}
}


Par Termino() :
{
	Par tipo = new Par(Symbol.Types.UNDEFINED, false);
	Par tipo2 = new Par(Symbol.Types.UNDEFINED, false);
}
{
    tipo = Factor() (Operador_multiplicativo() tipo2 = Factor()
	{
		// Se ha hecho matching con un operador SÓLO numérico, la expresión ya NO es sólo una variable
		tipo.segundo = false;
		if (tipo.primero != Symbol.Types.INT || tipo2.primero != Symbol.Types.INT) {
			System.out.println("ERROR: Se esperaba un tipo INT");
			tipo.primero = Symbol.Types.UNDEFINED;
		}
	})*
	{
		return tipo;
		//return sf.Termino(tipo, tipo2, match2);
	}
}


Par Factor() :
{
	Par tipo = new Par(Symbol.Types.UNDEFINED, false);
	Par tipo2 = new Par(Symbol.Types.UNDEFINED, false);
	boolean match = false;
}
{
	(tipo = Primario() | <tNOT> tipo2 = Primario() {match = true;})
	{
		if (match) {
			tipo.segundo = false;
			if (tipo2.primero != Symbol.Types.BOOL) {
				System.out.println("ERROR: Se esperaba un tipo BOOL");
				tipo.primero = Symbol.Types.UNDEFINED;
			}
			else tipo.primero = Symbol.Types.BOOL;
		}
		return tipo;
		//return sf.Factor(tipo, tipo2, match2);
	}
}


Par Primario() :
{
	Token t;
	Par tipo = new Par(Symbol.Types.UNDEFINED, false);
	Symbol simbolo = null;
}
{
    (<tAP> tipo = Expresion() <tCP>
|	LOOKAHEAD(2) t = <tID> <tAP> Expresion() (<tCOMA> Expresion())* <tCP>	//////////////////////////////////////////////////
|	t = <tID> {sf.Primario_ID(t, tipo, simbolo, st);}
|	t = <tNUM> {tipo.primero = Symbol.Types.INT;}
|	t = <tCONSTCHAR> {tipo.primero = Symbol.Types.CHAR;}
|	t = <tSTRING> {tipo.primero = Symbol.Types.STRING;}
|	t = <tTRUE> {tipo.primero = Symbol.Types.BOOL;}
|	t = <tFALSE> {tipo.primero = Symbol.Types.BOOL;})
	{return tipo;}
}
///////////////////////////////////////////////////////
///////////////////////////////////////////////////////
///////////////////////////////////////////////////////



void Asignacion() :
{
	Token id;
	Par tipo_asignacion;
}
{
	id = <tID> <tASIG> tipo_asignacion = Expresion()
	{sf.Asignacion(id.image, tipo_asignacion, st);}
}



void Instruccion_sin_return() :
{
	Token id;
	boolean llamada_funcion_simple = true;
}
{
	(LOOKAHEAD(2) Asignacion() // Asignacion a variable no array
	| LOOKAHEAD(2) id = <tID> (Instr_funcion_vector(id.image) {llamada_funcion_simple = false;})?
	{
		if (llamada_funcion_simple)
			sf.Instruccion(id.image, st);		// Llamada a función/procedimiento sin parámetros
			// Recordar casos skip_line y put_line()
	}
	| Instr_condicional_sin_return() | Instr_iterativa_sin_return()
	| <tNULL>) <tPC>
}



void Instruccion_con_return() :
{
	Token id;
	boolean llamada_funcion_simple = true;
}
{
	(LOOKAHEAD(2) Asignacion() // Asignacion a variable no array
	| LOOKAHEAD(2) id = <tID> (Instr_funcion_vector(id.image) {llamada_funcion_simple = false;})?
	{
		if (llamada_funcion_simple)
			sf.Instruccion(id.image, st);		// Llamada a función/procedimiento sin parámetros
			// Recordar casos skip_line y put_line()
	}
	| Instr_condicional_con_return() | Instr_iterativa_con_return()
	| <tNULL>
	| <tRETURN> Expresion()) <tPC>
}



void Instr_funcion_vector(String id) :
{
	Par tipo;
}
{
	<tAP> tipo = Expresion() Instr_funcion_vector_2(id, tipo)
}



void Instr_funcion_vector_2(String id, Par tipo) :
{
	Par parametro;
	ArrayList<Par> lista_parametros_llamada = null;
}
{
	<tCP> Instr_funcion_vector_3(id, tipo)
	|	// Matching de las llamadas a funciones con más de un parámetro
	{
		// Se crea la lista de parámetros y se añade el primero ya leído
		lista_parametros_llamada = new ArrayList<>();
		lista_parametros_llamada.add(tipo);
	}
	(<tCOMA> parametro = Expresion()
	{
		// Se recogen los parámetros que se leen
		lista_parametros_llamada.add(parametro);
	})+ <tCP>
	{
		// Se verifican los parámetros
		try {
			Symbol simbolo_llamada = st.getSymbol(id);

			if (simbolo_llamada.type == Symbol.Types.FUNCTION || simbolo_llamada.type == Symbol.Types.PROCEDURE) {
				ArrayList<Symbol> lista_parametros;
				if (simbolo_llamada.type == Symbol.Types.FUNCTION) {
					SymbolFunction s = (SymbolFunction) simbolo_llamada;
					lista_parametros = s.parList;
				}
				else {
					SymbolProcedure s = (SymbolProcedure) simbolo_llamada;
					lista_parametros = s.parList;
				}

				for (int i = 0; i < lista_parametros.size(); i++) {
					Symbol simbolo_parametro = lista_parametros.get(i);
					Par argumento = lista_parametros_llamada.get(i);
					if (argumento.primero != simbolo_parametro.type) {
						System.out.println("ERROR: Se esperaba un tipo " + simbolo_parametro.type);
					}
					if (simbolo_parametro.parClass == Symbol.ParameterClass.REF && !argumento.segundo) {
						System.out.println("ERROR: El parámetro " + simbolo_parametro.name + " es un parámetro por referencia");
					}
					/*	CASO ESPECIAL DE ARRAYS
					if (simbolo_parametro.type == Symbol.Types.ARRAY) {
						SymbolArray array_argumento = (SymbolArray) (st.getSymbol(argumento));
						SymbolArray array_parametro = (SymbolArray) ();
						if () {
							System.out.println("ERROR: Los índices del array parámetro " + simbolo_parametro.name + " no coinciden");
						}
						if () {
							System.out.println("ERROR: Los tipos base del array parámetro " + simbolo_parametro.name + " no coinciden");
						}
					}
					*/
				}
			}
			else {
				System.out.println("ERROR: Se esperaba un tipo procedimiento/función");
			}
		}
		catch (SymbolNotFoundException s) {
			System.out.println("ERROR: El símbolo " + id + " no existe");
		}
	}
	// recordar casos get(), put() y put_line()
}



void Instr_funcion_vector_3(String id, Par tipo_primera_expresion) :
{
	Par tipo_asignacion;
}
{
	// Matching de asignacion a componente de array "tipo_primera_expresion" es el tipo del índice
	<tASIG> tipo_asignacion = Expresion()
	{
		sf.Instr_funcion_vector_3(id, tipo_primera_expresion, tipo_asignacion, st);
	}

	| {}
	{
		Symbol funcion = st.getSymbol(id);

	}				// Matching de llamada a funcion con un solo parametro	///////////////////////////////////////////////////
					// recordar casos get(), put() y put_line()
}



void Programa_completo() :
{
}
{
	<tPROC> <tID> <tIS>
	(Declaracion() <tPC>)*
	(Procedimiento() | Funcion())*
	<tBEGIN>
	{System.out.println(st.toString());}
	(Instruccion_sin_return())+
	//{System.out.println(st.toString());}
	<tEND> <tPC>
}



// Devuelve un Symbol del tipo en cuestión, por valor o por referencia y sin nombre
Symbol Parametro_aux() :
{
	boolean por_referencia;
	Symbol tipo_parametro;
}
{
	({por_referencia = true;} <tREF> | {por_referencia = false;} {}) tipo_parametro = Tipos_variables() // Tipos_variables() devuelve UN symbol sin nombre de un tipo
	{
		if (por_referencia) {	// Se modifica el Symbol para indicar que es un parametro por referencia o por valor
			tipo_parametro.parClass = Symbol.ParameterClass.REF;
		}
		else {
			tipo_parametro.parClass = Symbol.ParameterClass.VAL;
		}
		return tipo_parametro;
	}
}



// Array por referencia con los símbolos leídos de parámetros
void Parametro(ArrayList<Symbol> lista_parametros) :
{
	ArrayList<String> array_nombres_variables = new ArrayList<>();
	Symbol tipo_parametros;
}
{
	Lista_IDS(array_nombres_variables) <tDOSP> tipo_parametros = Parametro_aux()
	{
		// Misma lógica que Declaracion(), se recoge un puntero a símbolo sin nombre y una lista de Strings,
		// se le asigna un nombre al puntero y se mete una copia en el array de simbolos
		for (String nombre_parametro : array_nombres_variables) {
			tipo_parametros.name = nombre_parametro;
			lista_parametros.add(tipo_parametros.clone());
		}
	}
}



// Similar a Declaracion(), inserta en la tabla de simbolos un array de símbolos (la lista de parametros) y lo retorna
ArrayList<Symbol> Parametros() :
{
	ArrayList<Symbol> lista_parametros = new ArrayList<>();
}
{
	(<tAP> Parametro(lista_parametros) (<tPC> Parametro(lista_parametros))* <tCP>)?
	{
		for (Symbol simbolo : lista_parametros) {
			try {st.insertSymbol(simbolo);}
			catch (AlreadyDefinedSymbolException a) {System.out.println("El simbolo " + simbolo.name + " ya esta definido (NO)");}
			catch (IndexArrayNotCorrect i) {System.out.println("Los indices del array " + simbolo.name + " no son correctos");}
		}
		return lista_parametros;
	}
}



void Procedimiento() :
{
	Token nombre_procedimiento;
	SymbolProcedure simbolo_procedimiento;
}
{
	<tPROC> nombre_procedimiento = <tID>
	// Antes de leer la lista de parametros se inserta el simbolo procedimiento y se crea el bloque de parametros y variables locales
	{
		// Se crea el simbolo procedimiento sin parametros, que van un nivel por debajo
		simbolo_procedimiento = new SymbolProcedure(nombre_procedimiento.image, new ArrayList<Symbol>());
		try {st.insertSymbol(simbolo_procedimiento);}
		catch (AlreadyDefinedSymbolException a) {System.out.println("El procedimiento " + nombre_procedimiento.image + " ya esta definido");}
		catch (IndexArrayNotCorrect i) {}	// Se sabe que el simbolo no es un array
		st.insertBlock();
	}
	simbolo_procedimiento.parList = Parametros() <tIS>	// Se asigna al simbolo del procedimiento la lista de parametros
	(Declaracion() <tPC>)*
	(Procedimiento() | Funcion())*
	<tBEGIN>
	(Instruccion_sin_return())+
	//{System.out.println(st.toString());}
	{st.removeBlock();}
	<tEND> <tPC>
}



// Igual a Procedimiento(), sólo cambia el return
void Funcion() :
{
	Token nombre_funcion;
	Symbol tipo_return;
	SymbolFunction simbolo_funcion;
}
{
	<tFUNC> nombre_funcion = <tID>
	{
		simbolo_funcion = new SymbolFunction(nombre_funcion.image, new ArrayList<Symbol>(), Symbol.Types.UNDEFINED);
		try {st.insertSymbol(simbolo_funcion);}
		catch (AlreadyDefinedSymbolException a) {System.out.println("La funcion " + nombre_funcion.image + " ya esta definida");}
		catch (IndexArrayNotCorrect i) {}
		st.insertBlock();
	}
	// Se asigna la lista de parámetros, se recoge el tipo que retorna la función y se asigna al simbolo función
	simbolo_funcion.parList = Parametros() <tRETURN> tipo_return = Tipo_variable() <tIS> {simbolo_funcion.returnType = tipo_return.type;}
	(Declaracion() <tPC>)*
	(Procedimiento() | Funcion())*
	<tBEGIN>
	(Instruccion_con_return())+
	//{System.out.println(st.toString());}
	{st.removeBlock();}
	<tEND> <tPC>
}



void Instr_iterativa_sin_return():
{
	Par tipo;
}
{
	<tWHILE> tipo = Expresion() <tLOOP>
	{
		if (tipo.primero != Symbol.Types.BOOL) {
			System.out.println("ERROR: Se esperaba una expresión booleana");
		}
	}
	(Instruccion_sin_return())+
	<tEND> <tLOOP>
}



void Instr_condicional_sin_return():
{
	Par tipo;
}
{
	<tIF> tipo = Expresion() <tTHEN>
	{
		if (tipo.primero != Symbol.Types.BOOL) {
			System.out.println("ERROR: Se esperaba una expresión booleana");
		}
	}
	(Instruccion_sin_return())+
	MasIF()
	<tEND> <tIF>
}



void MasIF():
{
	Par tipo;
}
{
	<tELSIF> tipo = Expresion() <tTHEN>
	{
		if (tipo.primero != Symbol.Types.BOOL) {
			System.out.println("ERROR: Se esperaba una expresión booleana");
		}
	}
	(Instruccion_sin_return())+
	MasIF()
	| <tELSE> (Instruccion_sin_return())+
	| {}
}



void Instr_iterativa_con_return():
{
	Par tipo;
}
{
	<tWHILE> tipo = Expresion() <tLOOP>
	{
		if (tipo.primero != Symbol.Types.BOOL) {
			System.out.println("ERROR: Se esperaba una expresión booleana");
		}
	}
	(Instruccion_con_return())+
	<tEND> <tLOOP>
}



void Instr_condicional_con_return():
{
	Par tipo;
}
{
	<tIF> tipo = Expresion() <tTHEN>
	{
		if (tipo.primero != Symbol.Types.BOOL) {
			System.out.println("ERROR: Se esperaba una expresión booleana");
		}
	}
	(Instruccion_con_return())+
	MasIF_return()
	<tEND> <tIF>
}



void MasIF_return():
{
	Par tipo;
}
{
	<tELSIF> tipo = Expresion() <tTHEN>
	{
		if (tipo.primero != Symbol.Types.BOOL) {
			System.out.println("ERROR: Se esperaba una expresión booleana");
		}
	}

	(Instruccion_con_return())+
	MasIF_return()
	| <tELSE> (Instruccion_con_return())+
	| {}
}
